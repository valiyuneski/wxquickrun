; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=wxQuickRun
AppVerName=wxQuickRun 0.95
AppPublisher=Priyank Bolia.
AppPublisherURL=http://www.priyank.co.in/wxQuickRun/
AppSupportURL=http://www.priyank.co.in/wxQuickRun/
AppUpdatesURL=http://www.priyank.co.in/wxQuickRun/
DefaultDirName={pf}\wxQuickRun
DefaultGroupName=wxQuickRun
AllowNoIcons=yes
LicenseFile=..\..\wxQuickRun\Setup\gpl.rtf
InfoBeforeFile=..\..\wxQuickRun\Setup\readme.rtf
OutputDir=..\..\wxQuickRun\Setup
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
VersionInfoVersion=0.9.5.0
VersionInfoTextVersion=0.95
AppCopyright=Copyright (C) 2006-2007 Priyank Bolia. All rights reserved.

[Languages]
Name: eng; MessagesFile: compiler:Default.isl

[Tasks]
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Dirs]
Name: {app}\en\; Flags: uninsalwaysuninstall; Tasks: ; Languages: 

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: ..\wxQuickRun\Release\wxQuickRun.exe; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,5.0.2195; Tasks: ; Languages: 
Source: ..\wxQuickRun\Release\wxQuickRun.exe.manifest; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,5.0.2195; Tasks: ; Languages: 
Source: ..\wxQuickRun\Release\sqlite3.dll; DestDir: {app}; Flags: ignoreversion; MinVersion: 0,5.0.2195
Source: ..\wxQuickRun\Release\en\wxQuickRun.mo; DestDir: {app}\en\; DestName: wxQuickRun.mo; Flags: ignoreversion; Tasks: ; Languages: 
Source: ..\wxQuickRun\Release\en\en-US.aff; DestDir: {app}\en\; DestName: en-US.aff; Flags: ignoreversion; Tasks: ; Languages: 
Source: ..\wxQuickRun\Release\en\en-US.dic; DestDir: {app}\en\; DestName: en-US.dic; Flags: ignoreversion; Tasks: ; Languages: 
Source: 50comupd.exe; DestDir: {tmp}
Source: vcredist_x86.exe; DestDir: {tmp}

[INI]
Filename: {app}\wxQuickRun.url; Section: InternetShortcut; Key: URL; String: http://www.priyank.co.in/wxQuickRun/

[Icons]
Name: {group}\wxQuickRun; Filename: {app}\wxQuickRun.exe; Tasks: quicklaunchicon
Name: {group}\{cm:ProgramOnTheWeb,wxQuickRun}; Filename: {app}\wxQuickRun.url

[Run]
Filename: {tmp}\50comupd.exe; Parameters: /r:n /q:1; Check: ShouldInstallComCtlUpdate
Filename: {tmp}\vcredist_x86.exe; Parameters: /r:n /q:1; Check: ShouldInstallComCtlUpdate
Filename: {app}\wxQuickRun.exe; Description: {cm:LaunchProgram,wxQuickRun}; Flags: nowait postinstall skipifsilent

[InstallDelete]
Name: {tmp}\50comupd.exe; Type: files; Tasks: ; Languages: 
Name: {tmp}\vcredist_x86.exe; Type: files; Tasks: ; Languages: 

[UninstallDelete]
Name: {app}\wxQuickRun.url; Type: files
Name: {app}\wxQuickRun.dat; Type: files
Name: {app}\wxQuickRun.db; Type: files
Name: {app}\wxQuickRun.exe; Type: files
Name: {app}\wxQuickRun.exe.manifest; Type: files
Name: {app}\en\wxQuickRun.mo; Type: filesandordirs
Name: {app}\en\en-US.aff; Type: filesandordirs
Name: {app}\en\en-US.dic; Type: filesandordirs
Name: {app}\en\; Type: filesandordirs
Name: {app}\en\; Type: dirifempty
Name: {app}\wxbase28u_vc_custom.dll; Type: files
Name: {app}\wxsqlite3u.dll; Type: files
Name: {app}\sqlite3.dll; Type: files
Name: {app}\MSVCP80.DLL; Type: files
Name: {app}\MSVCR80.DLL; Type: files
Name: {app}; Type: filesandordirs

[Registry]
Root: HKCU; Subkey: Software\Microsoft\Windows\CurrentVersion\Run; ValueType: string; ValueName: wxQuickRun; ValueData: {app}\wxQuickRun.exe; Flags: noerror uninsdeletevalue; Tasks: ; Languages: 
Root: HKCU; Subkey: Software\Priyank Bolia\wxQuickRun; ValueType: string; ValueName: Version; ValueData: 0.95; Flags: noerror uninsdeletevalue; Tasks: ; Languages: 
Root: HKCU; Subkey: Software\Priyank Bolia\wxQuickRun; ValueType: string; ValueName: Uninstaller; ValueData: {uninstallexe}; Flags: noerror uninsdeletevalue; Tasks: ; Languages: 

[Code]
function ShouldInstallComCtlUpdate: Boolean;
var
  MS, LS: Cardinal;
begin
  // Only install if the existing comctl32.dll is < 5.80
  Result := False;
  if GetVersionNumbers(ExpandConstant('{sys}\comctl32.dll'), MS, LS) then
    if MS < $00050050 then
      Result := True;
end;

function InitializeUninstall(): Boolean;
var hWnd: Longint;
begin
    hWnd := FindWindowByClassName('wxWindowClassNR');
    if hWnd > 0 then
    begin
		MsgBox( 'wxQuickRun is currently running.' + #13 + 'Please close it and start again this uninstaller.', mbCriticalError, MB_OK );
		// cannot go with setup anyway
		Result := False;
    end
    else begin
		Result := True;
	end;
end;

function InitializeSetup(): Boolean;
var hWnd: Longint;
	Uninstaller: String;
	ResultCode: Integer;
begin
    hWnd := FindWindowByClassName('wxWindowClassNR');
    if hWnd > 0 then
    begin
		MsgBox( 'wxQuickRun is currently running.' + #13 + 'Please close it and start again this setup.', mbCriticalError, MB_OK );
		// cannot go with setup anyway
		Result := False;
    end
    else begin
		Result := True;
	end;
	if RegValueExists(HKEY_CURRENT_USER, 'Software\Priyank Bolia\wxQuickRun', 'Uninstaller') then
	begin
		if RegQueryStringValue(HKEY_CURRENT_USER, 'Software\Priyank Bolia\wxQuickRun', 'Uninstaller', Uninstaller) then
		begin
			if(MsgBox('A previous version of wxQuickRun is already installed.' + #13 + 'Do you want to uninstall it, before proceeding?', mbInformation, MB_YESNO) = IDYES) then
			begin
				Exec(Uninstaller, '/VERYSILENT', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
			end;
		end;
	end;
end;
